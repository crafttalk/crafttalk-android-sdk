apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'maven-publish'

kapt {
    javacOptions {
        option("-source", "8")
        option("-target", "8")
    }
}

android {
    compileSdkVersion 34
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 34
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

ext {
    appCompatVersion = '1.1.0'
    coreVersion = '1.5.0'
    constraintlayoutVersion = '2.1.4'
    lifecycleVersion = '2.2.0'
    androidXRecyclerViewVersion = '1.2.1'
    materialVersion = '1.4.0'
    fragmentKtxVersion = '1.3.4'
    activityKtxVersion = '1.7.0'
    socketVersion = '2.1.0'
    retrofitVersion = '2.9.0'
    coroutinesVersion = '1.6.4'
    glideVersion = '4.13.2'
    roomVersion = '2.2.4'
    okHttpVersion = '4.10.0'
    kotlin_version = '1.7.10'
    daggerVersion = '2.43.2'
    firebaseMessagingServiceVersion = '23.0.8'
    firebaseAnalyticsVersion = '21.1.1'
    paginationVersion = '3.1.1'
    photoViewVersion = '2.3.0'
    securityPreferencesVersion = '1.1.0-alpha03'
    inputMaskVersion = '6.0.0'
    junitVersion = '4.13.2'
    testRulesVersion = '1.4.0'
    testRunnerVersion = '1.4.0'
    espressoVersion = '3.4.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.core:core-ktx:$coreVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayoutVersion"
    implementation "androidx.recyclerview:recyclerview:$androidXRecyclerViewVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.fragment:fragment-ktx:$fragmentKtxVersion"
    implementation "androidx.activity:activity-ktx:$activityKtxVersion"

    // DI
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
//    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    // OkHttp
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
    // Socket
    implementation "io.socket:socket.io-client:$socketVersion"
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycleVersion"
    // Push
    implementation "com.google.firebase:firebase-messaging:$firebaseMessagingServiceVersion"
    implementation "com.google.firebase:firebase-analytics:$firebaseAnalyticsVersion"
    // Pagination
    implementation "androidx.paging:paging-runtime:$paginationVersion"
    // Zooming photo
    implementation "com.github.chrisbanes:PhotoView:$photoViewVersion"
    // EncryptedSharedPreferences
    implementation "androidx.security:security-crypto:$securityPreferencesVersion"
    // mask
    implementation "com.github.RedMadRobot:input-mask-android:$inputMaskVersion"

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test:rules:$testRulesVersion"
    androidTestImplementation "androidx.test:runner:$testRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                group = 'com.github.crafttalk'
                artifactId = 'chat-client-android'
                version = '1.1.0'

                pom {
                    name = 'chat-client-android'
                    url = 'https://github.com/crafttalk/crafttalk-android-sdk'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'cactus666'
                            name = 'Ivan Shust'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com/crafttalk/crafttalk-android-sdk.git'
                        developerConnection = 'scm:git@github.com/crafttalk/crafttalk-android-sdk.git'
                        url = 'https://github.com/crafttalk/crafttalk-android-sdk'
                    }
                }
            }
        }
    }
}