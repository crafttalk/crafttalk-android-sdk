apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

ext {
    appCompat = '1.1.0'
    coreVersion = '1.3.0'
    constraintLayout = '1.1.3'
    testRunner = '1.0.2'
    lifecycleVersion = '2.2.0'
    archVersion = '2.1.0'
    androidXRecyclerViewVersion = '1.1.0'
    materialVersion = '1.1.0'
    fragmentKtxVersion = '1.3.0-alpha06'
    activityKtxVersion = '1.2.0-alpha06'
    socketVersion = '0.6.0'
    retrofitVersion = '2.8.1'
    coroutinesVersion = '1.3.0'
    glideVersion = '4.11.0'
    roomVersion = '2.2.4'
    okHttpVersion = '3.12.8'
    kotlin_version = '1.3.61'
    daggerVersion = '2.27'
    firebaseMessagingServiceVersion = '20.2.4'
    firebaseAnalyticsVersion = '17.5.0'
    paginationVersion = '2.1.2'
    photoViewVersion = '2.3.0'
    securityPreferencesVersion = '1.1.0-alpha03'
    constraintlayoutVersion = '1.1.3'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.core:core-ktx:${coreVersion}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:${appCompat}"
    implementation "androidx.constraintlayout:constraintlayout:${constraintlayoutVersion}"
    implementation "androidx.recyclerview:recyclerview:${androidXRecyclerViewVersion}"
    implementation "com.google.android.material:material:${materialVersion}"
    implementation "androidx.fragment:fragment-ktx:$fragmentKtxVersion"

    // DI
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    // Room
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    // Glide
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
//    implementation "com.squareup.retrofit2:converter-scalars:$retrofitVersion"
    // OkHttp
    implementation("com.squareup.okhttp3:okhttp:${okHttpVersion}") {
        force = true
    }
    implementation "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
    // Socket
    implementation "com.github.nkzawa:socket.io-client:$socketVersion"
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycleVersion"
    // Push
    implementation "com.google.firebase:firebase-messaging:$firebaseMessagingServiceVersion"
    implementation "com.google.firebase:firebase-analytics:$firebaseAnalyticsVersion"
    // Pagination
    implementation "android.arch.paging:runtime:$paginationVersion"
    // Zooming photo
    implementation "com.github.chrisbanes:PhotoView:$photoViewVersion"
    // EncryptedSharedPreferences
    implementation "androidx.security:security-crypto:$securityPreferencesVersion"

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                group = 'com.github.crafttalk'
                artifactId = 'chat-client-android'
                version = '1.1.0'

                pom {
                    name = 'chat-client-android'
                    url = 'https://github.com/crafttalk/crafttalk-android-sdk'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'cactus666'
                            name = 'Ivan Shust'
                        }
                    }
                    scm {
                        connection = 'scm:git@github.com/crafttalk/crafttalk-android-sdk.git'
                        developerConnection = 'scm:git@github.com/crafttalk/crafttalk-android-sdk.git'
                        url = 'https://github.com/crafttalk/crafttalk-android-sdk'
                    }
                }
            }
        }
    }
}